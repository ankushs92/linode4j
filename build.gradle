buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
    }
}
final String PHASE = "PRODUCTION"
group 'in.ankushs'
version '1.1'

apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'maven'

if(PHASE == 'PRODUCTION'){
	apply plugin: 'signing'
}

jar{
	baseName = "linode4j"
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {

    compile group: 'com.google.guava', name: 'guava', version: '23.5-jre'

    //logging
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'ch.qos.logback', name: 'logback-core', version: '1.2.3'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    //Lombok
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.18'

    //Jackson for JSON
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.2'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.2'

    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.9.1'

    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
}


jacocoTestReport {
    group = "Reporting"
    reports {
        xml.setEnabled(true)
        csv.setEnabled(false)
        html.setEnabled(false)
        html.destination "${buildDir}/reports/coverage"
    }
}

if(PHASE == 'PRODUCTION'){

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }


    uploadArchives {
        repositories {
            mavenDeployer{
                beforeDeployment{MavenDeployment deployment->
                    signing.signPom(deployment)
                }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom{
                    project{
                        name 'linode4j'
                        description "Java API for Linode's V4 REST API"
                        packaging 'jar'
                        url 'https://github.com/ankushs92/linode4j/issues'
                        inceptionYear '2017'

                        scm {
                            connection 'scm:git:git@github.com:ankushs92/linode4j.git'
                            developerConnection 'scm:git:git@github.com:ankushs92/linode4j.git'
                            url 'git@github.com:ankushs92/linode4j.git'
                        }

                        licenses {
                            license {
                                name 'The MIT License'
                                url 'https://github.com/ankushs92/linode4j/blob/master/LICENSE.md'
                            }
                        }
                        developers {
                            developer {
                                name 'Ankush Sharma'
                                email 'ankush20058@gmail.com'
                            }
                        }
                        issueManagement{
                            system 'Github'
                            url 'https://github.com/ankushs92/linode4j/issues'

                        }
                    }
                }
            }
        }
    }
}
